apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
apply plugin: 'dagger.hilt.android.plugin'

repositories {
    maven { url 'https://jitpack.io' }
}

apply from: 'jacoco.gradle'

android {
    compileSdkVersion 30
    buildToolsVersion "30.0.3"

    defaultConfig {
        applicationId "io.bloco.circles"
        minSdkVersion 23
        targetSdkVersion 30
        versionCode 6
        versionName "0.3"

        testInstrumentationRunner "io.bloco.circles.utils.TestRunner"
    }

    kapt {
        generateStubs = true
    }

    lintOptions {
        disable "UnsafeExperimentalUsageError",
                "UnsafeExperimentalUsageWarning"
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    // Share code between test environments
    sourceSets {
        String sharedTestDir = 'src/sharedTest/java'
        test {
            java.srcDir sharedTestDir
        }
        androidTest {
            java.srcDir sharedTestDir
        }
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }

    packagingOptions {
        exclude 'META-INF/*'
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
}

dependencies {
    //Kotlin
    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:1.4.1"

    // Android
    implementation 'androidx.core:core-ktx:1.6.0-beta02'
    implementation 'androidx.appcompat:appcompat:1.3.0'
    implementation 'androidx.coordinatorlayout:coordinatorlayout:1.1.0'
    implementation 'androidx.preference:preference-ktx:1.1.1'
    implementation 'androidx.security:security-crypto:1.1.0-alpha03'
    implementation 'androidx.lifecycle:lifecycle-viewmodel-ktx:2.3.1'
    implementation 'androidx.lifecycle:lifecycle-runtime-ktx:2.3.1'
    implementation 'androidx.constraintlayout:constraintlayout:2.0.4'

    // FlowBinding
    implementation "io.github.reactivecircus.flowbinding:flowbinding-android:$flowbinding_version"
    implementation "io.github.reactivecircus.flowbinding:flowbinding-material:$flowbinding_version"

    // Preferences
    implementation 'com.github.tfcporciuncula:flow-preferences:1.3.2'

    // Okhttp
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    implementation 'com.squareup.retrofit2:converter-gson:2.9.0'
    implementation "com.squareup.retrofit2:converter-scalars:2.4.0"
    implementation 'com.github.MohammadSianaki:Retrofit2-Flow-Call-Adapter:0.2.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:5.0.0-alpha.2'

    // Blockstack
    //noinspection GradleDependency
    implementation "com.github.blocoio:blockstack-android:$blockstack_version"

    implementation "com.github.komputing.kethereum:base58:$kethereum_version"
    implementation "com.github.komputing.kethereum:hashes:$kethereum_version"
    implementation "com.github.komputing.kethereum:crypto_api:$kethereum_version"
    implementation "com.github.komputing.kethereum:model:$kethereum_version"

    implementation ("com.github.komputing.kethereum:bip32:$kethereum_version") {
        exclude group:'com.github.komputing.KHash'
    }
    implementation "com.github.komputing.kethereum:bip44:$kethereum_version"
    implementation ("com.github.komputing.kethereum:bip39:$kethereum_version") {
        exclude group:'com.github.komputing.KHash'
    }
    implementation "com.github.komputing.kethereum:bip39_wordlist_en:$kethereum_version"

    implementation ("com.github.uport-project.kotlin-did-jwt:jwt:$did_jwt_version") {
        exclude group:'com.github.komputing.KHash'
        exclude group: 'com.squareup.okhttp3'
    }

    // Material Design
    implementation 'com.google.android.material:material:1.3.0'

    // Glide
    implementation 'com.github.bumptech.glide:glide:4.12.0'
    annotationProcessor 'com.github.bumptech.glide:compiler:4.12.0'

    // Dependency Injection
    implementation "com.google.dagger:hilt-android:$dagger_hilt_version"
    kapt "com.google.dagger:hilt-compiler:$dagger_hilt_version"
    kapt "androidx.hilt:hilt-compiler:$androidx_hilt_version"
    implementation "androidx.hilt:hilt-lifecycle-viewmodel:$androidx_hilt_version"

    // Logging
    implementation 'com.jakewharton.timber:timber:4.7.1'

    // Testing
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.json:json:20190722'
    testImplementation 'org.mockito:mockito-inline:3.11.0'
    testImplementation 'com.nhaarman.mockitokotlin2:mockito-kotlin:2.2.0'
    testImplementation "com.google.dagger:hilt-android-testing:$dagger_hilt_version"
    kaptTest "com.google.dagger:hilt-android-compiler:$dagger_hilt_version"

    // Instrumentation Testing
    androidTestImplementation 'androidx.test.ext:junit:1.1.2'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.3.0'
    androidTestImplementation  "com.google.dagger:hilt-android-testing:$dagger_hilt_version"
    kaptAndroidTest "com.google.dagger:hilt-android-compiler:$dagger_hilt_version"

}

tasks.withType(org.jetbrains.kotlin.gradle.tasks.KotlinCompile).all {
    kotlinOptions.freeCompilerArgs += [
            '-Xuse-experimental=kotlinx.coroutines.ExperimentalCoroutinesApi',
            '-Xuse-experimental=kotlinx.coroutines.FlowPreview',
            '-Xallow-result-return-type',
    ]
}
